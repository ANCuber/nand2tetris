class Trap {
    field int x, y; // position, top-left
    field int len, thick, diff;

    constructor Trap new(int xVal, int yVal) {
        let x = xVal;
        let y = yVal+7;
        let len = 60;
        let thick = 3;
        let diff = 7;
        do drawTrap();
        return this;
    }
    
    method void drawTrap() {
        do onScreen(1);
        return;
    }
    
    method void clearTrap() {
        do onScreen(0);
        return;
    }
    
    method void onScreen(int ctrl) {
        var int cnt;
        let cnt = 0;
        
        do Screen.setColor(ctrl);
        do Screen.drawRectangle(x, y, x+len, y+thick-1);
        do Screen.drawRectangle(x+diff, y+thick-1, x+len-diff, y+thick);
        
        while (cnt < len) {
            do Screen.drawLine(x+cnt+1, y-1, x+cnt+5, y-1);
            do Screen.drawLine(x+cnt+2, y-2, x+cnt+4, y-2);
            do Screen.drawPixel(x+cnt+3,y-3);
            let cnt = cnt+6;
        }
        
        return;
    }

    method void moveUp(int dh) {
        do clearTrap();
        let y = y-dh;
        do drawTrap();
        return;
    }
}