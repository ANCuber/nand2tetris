class Circle {
    field int x, y, size; // center of mass
    field int ax, ay; // acceleration
    field int vx, vy; // velocity
    field int angle;
    constructor Circle new(int sx, int sy, int ssize, int sax, int say, int svx, int svy, int sangle) {
        let x = sx;
        let y = sy;
        let size = ssize;
        let ax = sax;
        let ay = say;
        let vx = svx;
        let vy = svy;
        let angle = sangle;
        return this;
    }
    method int getx() { return x; }
    method int gety() { return y; }
    method int getAngle() { return angle; }
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, size);
        return;
    }
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, size);
        return;
    }
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
    method void moveUp() {
        if(y-2-size > -1) {
            do erase();
            let y = y-2;
            do draw();
            do Sys.wait(3);
        }
        return;
    }
    method void moveDown() {
        if(y+2+size < 256) {
            do erase();
            let y = y+2;
            do draw();
            do Sys.wait(3);
        }
        return;
    }
    method void moveRight() {
        if(x+2+size < 512) {
            do erase();
            let x = x+2;
            do draw();
            do Sys.wait(3);
        }
        return;
    }
    method void moveLeft() {
        if(x-2-size > -1) {
            do erase();
            let x = x - 2;
            do draw();
            do Sys.wait(3);
        }
        return;
    }
    method void incAngle() {
        if (angle < 175) {
            let angle = angle + 6;
        }
        return;
    }
    method void decAngle() {
        if (angle > 5) {
            let angle = angle - 6;
        }
        return;
    }
    method void printInfo() {
        do Screen.clearScreen();
        do draw();
        do Output.moveCursor(0, 0);
        do Output.printString("angle: ");
        do Output.printInt(angle);
        do Output.println();
        
        do Output.printString("(vx, vy): ");
        do Output.printString("(");
        do Output.printInt(vx);
        do Output.printString(", ");
        do Output.printInt(vy);
        do Output.printString(")");
        do Output.println();

        do Output.printString("(ax, ay): ");
        do Output.printString("(");
        do Output.printInt(ax);
        do Output.printString(", ");
        do Output.printInt(ay);
        do Output.printString(")");
        do Sys.wait(500);
        return;
    }
}