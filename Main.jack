class Main {
    // function int mod(int a, int b) {
    //     return (a)-(a/b)*
    // }
    function void main() {
        var Instance craft;
        var int key, dAX, dAY;
        var boolean burn;
        // Initialization with x, y, vx, vy, ax, ay, fuel, angle
        let craft = Instance.new(80, 30, 5, 0, 0, 1, 0, 0);
        do craft.draw(true);
        do Random.set_seed(11223);
        do Terrain.Init(151, 15, 50);
        do Data.Init();
        while(Terrain.checkCollision((craft.getx()), (craft.gety()), 7) = 0) {
            let burn = false;
            do craft.draw(false);
            let key = Keyboard.keyPressed();
            if(key = 130) { // press left
                do craft.decAngle();
            }
            if(key = 132) { // press right
                do craft.incAngle();
            }
            if(key = 131) { // press up
                let dAX = Data.getdAX(craft.getAngle()+15);
                let dAY = Data.getdAY(craft.getAngle()+15);
                let burn = true;
                do craft.updateRate(dAX, dAY);
            }
            do craft.changePos(craft.getx()+craft.getvx(), craft.gety()+craft.getvy());
            do craft.updateVelocity();
			if(craft.getx() < 100){
				do Terrain.Shifting(100-craft.getx());
            	do craft.changePos(100, craft.gety());
			}
			if(craft.getx() > 412){
				do Terrain.Shifting(412-craft.getx());
            	do craft.changePos(412, craft.gety());
			}
			if(craft.gety()<10){
            	do craft.changePos(craft.getx(), 10);
			}
            if(burn) { do craft.updateRate(-dAX, -dAY); }
            do craft.draw(true);
            do Sys.wait(200);
        }
        return;
    }
}
