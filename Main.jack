class Main {
    function void main() {
        var Instance craft;
        var int key, dAX, dAY, score, fuel;
        var boolean burn;
        var Explode boom;
        do TextDisplay.start_screen();
        // Initialization with x, y, vx, vy, ax, ay, fuel, angle
        let craft = Instance.new(80, 30, 5, 0, 0, 1, 0, 0);
        let fuel = 500;
        do craft.draw(true);
        do Random.set_seed(1558);
        //do Terrain.Init(251, 15, 30);
        do Terrain.initDrawer(15,30);
        do Data.Init();
        let key = 0;
        let score=0;
        while(key=0){
            let score=score+1;
            if(score>30000){
                let score=0;
            }
            let key = Keyboard.keyPressed();
        }
        do Random.set_seed(Math.abs(key*score));
        let score=0;
        do Screen.clearScreen();
        do craft.draw(true);
        do Terrain.Init(251,15,30);
        while(Terrain.checkCollision((craft.getx()), (craft.gety()), 7) = 0) {
            let burn = false;
            let key = Keyboard.keyPressed();
            do craft.fire(false);
            do craft.draw(false);
            if(key = 130) { // press left
                do craft.decAngle();
            }
            if(key = 132) { // press right
                do craft.incAngle();
            }
            if((key = 131) & (fuel > 0)) { // press up
                let dAX = Data.getdAX(craft.getAngle()+15);
                let dAY = Data.getdAY(craft.getAngle()+15);
                let burn = true;
                let fuel = fuel - 1;
                do craft.updateRate(dAX, dAY);
            }
            do craft.changePos(craft.getx()+craft.getvx(), craft.gety()+craft.getvy());
            do craft.updateVelocity();
			if(craft.getx() < 100){
				do Terrain.Shifting(100-craft.getx());
            	do craft.changePos(100, craft.gety());
			}
			if(craft.getx() > 412){
				do Terrain.Shifting(412-craft.getx());
            	do craft.changePos(412, craft.gety());
			}
            if(craft.gety() < 10){
            	do craft.changePos(craft.getx(), 10);
			}
            do craft.displayInfo();
            do Output.printString("Fuel: ");
            do Output.printInt(fuel);
            if(burn) { do craft.updateRate(-dAX, -dAY); }
            do craft.draw(true);
            if(key = 131){
                 do craft.fire(true);
            }
            do Sys.wait(100);
            do craft.hideInfo();
        }
        let score = Terrain.checkCollision((craft.getx()), (craft.gety()), 7);
        if(score = -1) {
            do craft.draw(false);
            let boom = Explode.new(craft.getx(), craft.gety());
            do boom.explode();
            do Sys.wait(100);
            do Screen.clearScreen();
            do TextDisplay.fail_screen(craft.getx());
        } else {
            if((Math.abs(craft.getAngle())<4) & (craft.getvy() < 5)) {
                do Sys.wait(100);
                do Screen.clearScreen();
                do TextDisplay.successful_screen((score*100));
            }else{
                do craft.draw(false);
                let boom = Explode.new(craft.getx(), craft.gety());
                do boom.explode();
                do Sys.wait(100);
                do Screen.clearScreen();
                do TextDisplay.fail_screen(craft.getx());
            }
        }
        return;
    }
}
